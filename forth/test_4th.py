class TestForth:
    scenarios = r"""
: / /MOD SWAP DROP ;
: '\n' 10 ;
: BL 32 ;
: CR '\n' EMIT ;
: SPACE BL EMIT ;
: NEGATE 0 SWAP - ;
: TRUE  1 ;
: FALSE 0 ;
: NOT   0= ;
: LITERAL IMMEDIATE ' LIT , , ;
: ':' [ CHAR : ] LITERAL ;
: ';' [ CHAR ; ] LITERAL ;
: '"' [ CHAR " ] LITERAL ;
: 'A' [ CHAR A ] LITERAL ;
: '0' [ CHAR 0 ] LITERAL ;
: '-' [ CHAR - ] LITERAL ;
: [COMPILE] IMMEDIATE WORD FIND >CFA , ;
: RECURSE   IMMEDIATE LATEST @ >CFA , ;
: IF        IMMEDIATE ' 0BRANCH , HERE @ 0 , ;
: THEN      IMMEDIATE DUP HERE @ SWAP - SWAP ! ;
: ELSE      IMMEDIATE ' BRANCH , HERE @ 0 , SWAP DUP HERE @ SWAP - SWAP ! ;
: BEGIN     IMMEDIATE HERE @ ;
: AGAIN     IMMEDIATE ' BRANCH , HERE @ - , ;
: WHILE     IMMEDIATE ' 0BRANCH , HERE @ 0 , ;
: REPEAT    IMMEDIATE ' BRANCH , SWAP HERE @ - , DUP HERE @ SWAP - SWAP ! ;
: NIP SWAP DROP ;
: PICK 1+ 8 * DSP@ + @ ;
: SPACES BEGIN DUP 0> WHILE SPACE 1- REPEAT DROP ;
: U. BASE @ /MOD ?DUP IF RECURSE THEN DUP 10 < IF '0' ELSE 10 - 'A' THEN + EMIT ;
: .S DSP@ BEGIN DUP S0 @ < WHILE DUP @ U. 8+ SPACE REPEAT DROP ;
: UWIDTH BASE @ / ?DUP IF RECURSE 1+ ELSE 1 THEN ;
: U.R SWAP DUP UWIDTH ROT SWAP - SPACES U. ;
: .R SWAP DUP 0< IF NEGATE 1 SWAP ROT 1- ELSE 0 SWAP ROT THEN SWAP DUP
  UWIDTH ROT SWAP - SPACES SWAP IF '-' EMIT THEN U. ;
: . 0 .R SPACE ;
: U. U. SPACE ;
: WITHIN -ROT OVER <= IF > IF TRUE ELSE FALSE THEN ELSE 2DROP FALSE THEN ;
: ALIGNED 7 + -8 AND ;
: ALIGN HERE @ ALIGNED HERE ! ;
: C, HERE @ C! 1 HERE +! ;
: S" IMMEDIATE STATE @ IF
  ' LITSTRING , HERE @ 0 , BEGIN KEY DUP '"' <> WHILE
  C, REPEAT DROP DUP HERE @ SWAP - 8- SWAP ! ALIGN ELSE
  HERE @ BEGIN KEY DUP '"' <> WHILE OVER C! 1+ REPEAT DROP HERE @ - HERE @ SWAP THEN ;
: ." IMMEDIATE STATE @ IF [COMPILE] S" ' TELL , ELSE BEGIN KEY DUP '"' = IF
  DROP EXIT THEN EMIT AGAIN THEN ;
: CELLS 8 * ;
: ID. 8+ DUP C@ F_LENMASK AND BEGIN DUP 0> WHILE SWAP 1+ DUP C@ EMIT SWAP 1- REPEAT
2DROP ;
: ?IMMEDIATE 8+ C@ F_IMMED AND ;
: CASE    IMMEDIATE 0 ;
: OF      IMMEDIATE ' OVER , ' = , [COMPILE] IF ' DROP , ;
: ENDOF   IMMEDIATE [COMPILE] ELSE ;
: ENDCASE IMMEDIATE ' DROP , BEGIN ?DUP WHILE [COMPILE] THEN REPEAT ;
: CFA> LATEST @ BEGIN ?DUP WHILE 2DUP >CFA = IF NIP EXIT THEN @ REPEAT DROP 0 ;
: SEE WORD FIND HERE @ LATEST @ BEGIN 2 PICK OVER <> WHILE NIP DUP @ REPEAT DROP SWAP
  ':' EMIT SPACE DUP ID. SPACE DUP ?IMMEDIATE IF ." IMMEDIATE " THEN >DFA
  BEGIN 2DUP > WHILE DUP @
      CASE
          ' LIT OF 8+ DUP @ . ENDOF
          ' LITSTRING OF [ CHAR S ] LITERAL EMIT '"' EMIT SPACE 8+ DUP @ SWAP 8+
            SWAP 2DUP TELL '"' EMIT SPACE + ALIGNED 8- ENDOF
          ' 0BRANCH OF ." 0BRANCH ( " 8+ DUP @ . ." ) " ENDOF
          '  BRANCH OF  ." BRANCH ( " 8+ DUP @ . ." ) " ENDOF
          ' ' OF [ CHAR ' ] LITERAL EMIT SPACE 8+ DUP CFA> ID. SPACE ENDOF
          ' EXIT OF 2DUP 8+ <> IF ." EXIT " THEN ENDOF
          DUP CFA> ID. SPACE
      ENDCASE
      8+
  REPEAT
 ';' EMIT CR 2DROP ;
: ['] IMMEDIATE ' LIT , ;
: EXCEPTION-MARKER RDROP 0 ;
: CATCH DSP@ 8+ >R ' EXCEPTION-MARKER 8+ >R EXECUTE ;
: THROW ?DUP IF RSP@ BEGIN DUP R0 8- < WHILE DUP @ ' EXCEPTION-MARKER 8+ = IF
  8+ RSP! DUP DUP DUP R> 8- SWAP OVER ! DSP! EXIT THEN 8+ REPEAT
  DROP CASE 0 1- OF ." ABORTED" CR ENDOF ." UNCAUGHT THROW " DUP . CR ENDCASE QUIT THEN
  ;
: Z" IMMEDIATE STATE @ IF
  ' LITSTRING , HERE @ 0 , BEGIN KEY DUP '"' <> WHILE
  HERE @ C! 1 HERE +! REPEAT 0 HERE @ C! 1 HERE +! DROP DUP
  HERE @ SWAP - 8- SWAP ! ALIGN ' DROP , ELSE
  HERE @ BEGIN KEY DUP '"' <> WHILE OVER C! 1+ REPEAT DROP 0 SWAP C! HERE @ THEN ;
: STRLEN DUP BEGIN DUP C@ 0<> WHILE 1+ REPEAT SWAP - ;
: ARGC (ARGC) @ ;
: ARGV 1+ CELLS (ARGC) + @ DUP STRLEN ;
: ENVIRON ARGC 2 + CELLS (ARGC) + ;
65 EMIT CR \ A
777 65 EMIT DROP CR \ A
32 DUP + 1+ EMIT CR \ A
16 DUP 2DUP + + + 1+ EMIT CR \ A
8 DUP * 1+ EMIT CR \ A
CHAR A EMIT CR \ A
: SLOW WORD FIND >CFA EXECUTE ; 65 SLOW EMIT CR \ A
3480240455236671827 DSP@ 8 TELL 2DROP CR \ SYSCALL0
3480240455236671827 DSP@ HERE @ 8 CMOVE HERE @ 8 TELL 2DROP CR \ SYSCALL0
13622 DSP@ 2 NUMBER DROP EMIT CR \ A
64 >R RSP@ 1 TELL RDROP CR \ @
64 DSP@ RSP@ SWAP C@C! RSP@ 1 TELL 2DROP CR \ @
64 >R 1 RSP@ +! RSP@ 1 TELL RDROP CR \ A
: <BUILDS WORD CREATE DODOES , 0 , ;
: DOES> R> LATEST @ >DFA ! ;
: CONST <BUILDS , DOES> @ ;
65 CONST FOO
FOO EMIT CR \ A
VERSION . CR \ 47
LATEST @ ID. CR \ FOO
0 1 > . CR \ 0
1 0 > . CR \ -1
0 1 >= . CR \ 0
0 0 >= . CR \ -1
0 0<> . CR \ 0
1 0<> . CR \ -1
1 0<= . CR \ 0
0 0 <= . CR \ -1
-1 0>= . CR \ 0
0 0>= . CR \ -1
0 0 OR . CR \ 0
0 -1 OR . CR \ -1
-1 -1 XOR . CR \ 0
0 -1 XOR . CR \ -1
-1 INVERT . CR \ 0
0 INVERT . CR \ -1
3 4 5 .S 2DROP DROP CR \ 5 4 3
F_IMMED F_HIDDEN .S 2DROP CR \ 32 128
: CFA@ WORD FIND >CFA @ ; CFA@ >DFA DOCOL = . CR \ -1
3 4 5 WITHIN . CR \ 0
102 SYSCALL0 . CR \ {uid}
{uid} 105 SYSCALL1 . CR \ 0
O_RDONLY Z" {argv0}" 21 SYSCALL2 . CR \ 0
S" test" SWAP 1 SYS_WRITE SYSCALL3 . CR \ test4
ARGC . CR \ 1
0 ARGV TELL CR \ {argv0}
ENVIRON @ DUP STRLEN TELL CR \ SHELL=/bin/bash
SEE >DFA \ : >DFA >CFA 8+ EXIT ;
SEE HIDE \ : HIDE WORD FIND HIDDEN ;
SEE QUIT \ : QUIT R0 RSP! INTERPRET BRANCH ( -16 ) ;
: FOO THROW ;
: TEST-EXCEPTIONS 25 ['] FOO CATCH ?DUP IF ." FOO threw exception: " . CR DROP THEN ;
TEST-EXCEPTIONS \ FOO threw exception: 25
"""

    def test_4th(self, actual: str, expected: str):
        assert actual.rstrip() == expected

    def test_5th(self, actual: str, expected: str):
        assert actual.rstrip() == expected
